import org.apache.tools.ant.taskdefs.condition.Os

/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

description = 'Ballerinax - Docker Extension Examples'

configurations {
   bDistributionDir
}

dependencies {
   bDistributionDir project(path: ":docker-extension-annotations", configuration: "bDistributionDir")
}

jar {
   enabled = false
}

task buildExamples {
   dependsOn configurations.bDistributionDir
   dependsOn ':docker-extension-test:prepareDistribution'
   doLast {
      if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
         def distributionLocation = configurations.bDistributionDir.singleFile
         // Example - "docker-deployment"
         exec {
            workingDir file("${project.projectDir}/examples/docker-deployment")
            commandLine 'sh', '-c', "$distributionLocation/bin/ballerina build docker_deployment.bal"
         }
         exec {
            commandLine 'sh', '-c', "docker rmi helloworld:v1.0"
         }
         file("${project.projectDir}/examples/docker-deployment/docker_deployment.jar").delete()
         file("${project.projectDir}/examples/docker-deployment/docker").deleteDir()

         // Example - "docker-deployment-without-annotations"
         exec {
            workingDir file("${project.projectDir}/examples/docker-deployment-without-annotations")
            commandLine 'sh', '-c', "$distributionLocation/bin/ballerina build docker_deployment_without_annotations.bal"
         }
         exec {
            commandLine 'sh', '-c', "docker rmi docker_deployment_without_annotations:latest"
         }
         file("${project.projectDir}/examples/docker-deployment-without-annotations/docker_deployment_without_annotations.jar").delete()
         file("${project.projectDir}/examples/docker-deployment-without-annotations/docker").deleteDir()
      }
   }
}

task zipExamples(type: Zip) {
   from 'examples/'
   include '*'
   include '*/*'
   archiveFileName = "docker-extension-examples-${ballerinaLangVersion}.zip"
   destinationDirectory = file(project.buildDir.absolutePath)
}

publishing {
   publications {
      mavenJava(MavenPublication) {
         artifact source: zipExamples, extension: 'zip'
      }
   }
}

zipExamples.dependsOn buildExamples
build.dependsOn zipExamples
